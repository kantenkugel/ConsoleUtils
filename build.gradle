plugins {
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.7.3'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '2.0.1'
    id 'com.github.ben-manes.versions' version '0.15.0'
}

group 'com.kantenkugel'
archivesBaseName = "ConsoleUtils"
version '1.0'

mainClassName = 'com.kantenkugel.consoleutils.ConsoleUtilTestMain'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    //JNA
    compile 'net.java.dev.jna:jna:4.5.0'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.8'
    testCompile 'org.powermock:powermock-module-junit4:1.7.1'
    testCompile 'org.powermock:powermock-api-mockito2:1.7.1'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        BintrayRelease(MavenPublication) {
            from components.java
            groupId group
            artifactId archivesBaseName
            version version
            artifact sourcesJar
        }
    }
}

bintray {
    user = getProjectProperty("bintrayUsername")
    key = getProjectProperty("bintrayApiKey")
    publications = ["BintrayRelease"]
    pkg {
        repo = 'maven'
        name = 'ConsoleUtils'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/kantenkugel/ConsoleUtils.git'
        publish = true
        version {
            name = project.version
            released = new Date()
        }
    }
}

String getProjectProperty(String propertyName)
{
    String property = ""
    if (hasProperty(propertyName))
    {
        property = this.properties[propertyName]
    }
    return property
}

assemble.dependsOn sourcesJar

build {
    dependsOn clean
    dependsOn jar
    dependsOn sourcesJar
    dependsOn shadowJar

    jar.mustRunAfter clean
    shadowJar.mustRunAfter sourcesJar
}

bintrayUpload {
    dependsOn build

    onlyIf { !getProjectProperty("bintrayUsername").empty }
    onlyIf { !getProjectProperty("bintrayApiKey").empty }
}